{
  function doInt(o) {
    return parseInt(o.join(""), 10);
  }

}

start 
  = nl* c:command trim? {return c}

trim
  = ([\n\t ]*) 


command
  =statement 
  / query   
  / hash 
  / halt 
  


halt
	= "halt"i 
  		{	
	  		return {
	  					command:'halt'
	  				}	 
	  	}

hash
	= "hash-threshold" _ maxResponses:[0-9]+ 
  		{
  			return {
  						command: 'setThreshold', 
  						maxResponses: doInt(maxResponses)
  					} 
  		}

statement
  = expect:statementExpectation _* nl sql:sql 
  	{
  		return {
  					command: 'execute', 
  					expectSucess: expect, 
  					sql:sql
  				}
  	}

statementExpectation
  = "statement"i ' ' v:statementOption {return v}

statementOption
  = "ok"i    {return true}
  / "fail"i  {return false}


sql
  = result:	(
				t:(!delimitor .) {return t[1];}
			)* 
		delimitor {return result.join("");}

delimitor
	= '\n----'
	/ !.

query
  = queryInit:queryInit? 
    queryMain:queryMain nl
    sql:sql nl
    result:result
    {
    	return {
			command: 'execute',
			expectSucess: true,
			sql: sql,
			result: result,
			skipif: queryInit.skipif || false,
			onlyif: queryInit.onlyif || false,
		}
    }

nl
	= "\n"

result
	= resultHash
	/ resultList
	/ resultVoid

resultHash
	= amount:$[0-9]+ 
		" values hashing to " 
		hash:alphanum 
		{
			return {
				type:'hash',
				amount: amount,
				hash:hash
			}
		}


resultList
	= list:(v:$[^\n]+ nl{return v})+
	{
		console.log(list)
		return {
			type:'list',
			values: list
		}
	}
	
resultVoid
	= nl* !. 
	{
		return{
			type:"void"
		}

	}
	

queryInit
  = c:queryInitCommand* 
  	{ 
  		var skipif = [];
  		var onlyif = [];
		for (var i = c.length - 1; i >= 0; i--) {
			if('skip' == c[i].type){
				skipif.push(c[i].val);
			} else if('only' == c[i].type){
				onlyif.push(c[i].val);
			}
		};
  		return {skipif:skipif,onlyif:onlyif}
  	}

queryInitCommand
  = "skipif"i ' ' v:alphanum nl {return {type:'skip', val: v}}
  / "onlyif"i ' ' v:alphanum nl {return {type:'only', val: v}}


queryMain
 = "query"i _ colInfo _ sortInfo

skipif
  = "skipif"i _ alphanum


colInfo
  = [SDI]+

sortInfo
  = "nosort"i
  / "rowsort"i


alphanum
  = $[a-z0-9]i+


_
	= ' '








